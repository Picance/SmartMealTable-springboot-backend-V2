plugins {
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

dependencies {
    // 모듈 의존성
    implementation project(':smartmealtable-core')
    implementation project(':smartmealtable-domain')
    implementation project(':smartmealtable-storage:db')
    implementation project(':smartmealtable-storage:cache')
    
    // Spring Boot Web
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    
    // Spring Transaction (JPA 없이 트랜잭션만 사용)
    implementation 'org.springframework:spring-tx'
    
    // BCrypt 암호화 (Spring Security 없이 사용)
    implementation 'at.favre.lib:bcrypt:0.10.2'
    
    // JWT 토큰 처리 (Spring Security 없이 사용)
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'
        
    // Spring Boot DevTools
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    
    // 테스트
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:mysql'
}

ext {
    set('snippetsDir', file("build/generated-snippets"))
}

tasks.named('test') {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

tasks.named('asciidoctor') {
    inputs.dir snippetsDir
    dependsOn test
    attributes 'snippets': snippetsDir
}

// 실행 가능한 jar 생성
tasks.named('bootJar') {
    enabled = true
    mainClass = 'com.stdev.smartmealtable.api.ApiApplication'
}

tasks.named('jar') {
    enabled = false
}
